table 'IM AOV Last 7 Days Line'
	lineageTag: a0ffbc2a-31db-4fe6-a7c3-19d90bb5adc4

	column COUNT_id
		dataType: double
		sourceProviderType: double
		lineageTag: 140edfce-7beb-4b51-bcee-97ac2d13275a
		summarizeBy: sum
		sourceColumn: COUNT_id

		annotation SummarizationSetBy = Automatic

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	column created_at_ist
		dataType: dateTime
		formatString: General Date
		sourceProviderType: datetime2
		lineageTag: 9e3f2a7c-164d-48af-930b-1d89fd04c798
		summarizeBy: none
		sourceColumn: created_at_ist

		annotation SummarizationSetBy = Automatic

	column time_period
		dataType: string
		sourceProviderType: nvarchar(max)
		lineageTag: 3d67a9c1-1b1d-407d-9e41-5209a98f87d3
		summarizeBy: none
		sourceColumn: time_period

		annotation SummarizationSetBy = Automatic

	column 'Day of Week'
		dataType: int64
		isNullable: false
		formatString: 0
		sourceProviderType: bigint
		lineageTag: 5040eb5c-8e85-44d7-b3c1-f177f12f5cce
		summarizeBy: sum
		sourceColumn: Day of Week

		annotation SummarizationSetBy = Automatic

	column Week
		dataType: string
		sourceProviderType: nvarchar(max)
		lineageTag: 548a60d6-af54-46be-b6f0-9aee127e9ecd
		summarizeBy: none
		sourceColumn: Week
		sortByColumn: created_at_ist

		changedProperty = SortByColumn

		annotation SummarizationSetBy = Automatic

	partition 'IM AOV Last 7 Days Line' = m
		mode: directQuery
		queryGroup: 'IM\IM-Last 7 Days\IM-Sales\IM_AOV'
		source = ```
				let
				   // Get current date and time in UTC
				    TodayDateTimeZone = DateTimeZone.UtcNow(),
				
				    // Convert UTC time to IST (Indian Standard Time, UTC +5:30)
				    TodaysDateTime = TodayDateTimeZone + #duration(0, 5, 30, 0),
				
				    // Get today's date in IST (removes the time part)
				
				    TodaysTime = Time.ToText(DateTime.Time(TodaysDateTime), "HH:mm:ss"),
				
				    TodayDate = DateTime.Date(TodaysDateTime),
				
				    // Get the date 7 days ago in IST
				    Last7Days = TodayDate - #duration(7, 0, 0, 0),
				
				    // Last 7 days current week to 12:00 AM IST
				    StartOfLast7Days = DateTime.FromText(Date.ToText(Last7Days) & " 00:00:00"),
				    StartOfLast7DaysFormatted = DateTime.ToText(StartOfLast7Days, "yyyy-MM-dd HH:mm:ss"),
				    CurrentDate = DateTime.FromText(Date.ToText(DateTime.Date(TodaysDateTime)) &  " 00:00:00"),
				    EndofCurrentDateFormatted = DateTime.ToText(CurrentDate, "yyyy-MM-dd HH:mm:ss"),
				
				    // Last 7 days previous week to 12:00 AM IST
				    LastWeekStartDateTime = StartOfLast7Days - #duration(7, 0, 0, 0),
				    StartOfLastWeek = DateTime.FromText(Date.ToText(DateTime.Date(LastWeekStartDateTime)) & " 00:00:00"),
				    StartOfLastWeekFormatted = DateTime.ToText(StartOfLastWeek, "yyyy-MM-dd HH:mm:ss"),
				    EndofLastWeekFormatted = StartOfLast7DaysFormatted,
				    // Reference the Table Name parameter dynamically
				    TableName = IMTableName,
				      // Reference all dynamic parameters
				    IMCityParam = if Value.Is(IM_City_Parameter, type list) then IM_City_Parameter else {IM_City_Parameter},
				    RegionParam = if Value.Is(Region_Parameter, type list) then Region_Parameter else {Region_Parameter},
				    ZoneParam = if Value.Is(Zone_Parameter, type list) then Zone_Parameter else {Zone_Parameter},
				    StoreIDParam = if Value.Is(StoreID_Parameter, type list) then StoreID_Parameter else {StoreID_Parameter},
				    DeliveryTypeParam = if Value.Is(DeliveryType_Parameter, type list) then DeliveryType_Parameter else {DeliveryType_Parameter},
				    DeliverySlotParam = if Value.Is(DeliverySlot_Parameter, type list) then DeliverySlot_Parameter else {DeliverySlot_Parameter},
				
				    // BUild Dynamic Conditions
				    IMCityCondition = if List.NonNullCount(IMCityParam) > 0 then " | where city in (" & Text.Combine(List.Transform(IMCityParam, each "'" & _ & "'"), ", ") & ")" else "",
				    RegionCondition = if List.NonNullCount(RegionParam) > 0 then " | where region in (" & Text.Combine(List.Transform(RegionParam, each "'" & _ & "'"), ", ") & ")" else "",
				    ZoneCondition = if List.NonNullCount(ZoneParam) > 0 then " | where zone in (" & Text.Combine(List.Transform(ZoneParam, each "'" & _ & "'"), ", ") & ")" else "",
				    StoreIDCondition = if List.NonNullCount(StoreIDParam) > 0 then " | where store_id in (" & Text.Combine(List.Transform(StoreIDParam, each "'" & _ & "'"), ", ") & ")" else "",
				    DeliveryTypeCondition = if List.NonNullCount(DeliveryTypeParam) > 0 then " | where delivery_type in (" & Text.Combine(List.Transform(DeliveryTypeParam, each "'" & _ & "'"), ", ") & ")" else "",
				    DeliverySlotCondition = if List.NonNullCount(DeliverySlotParam) > 0 then " | where delivery_slot in (" & Text.Combine(List.Transform(DeliverySlotParam, each "'" & _ & "'"), ", ") & ")" else "",
				    
				    // Combine All Filters
				    CombinedFilters = IMCityCondition & RegionCondition & ZoneCondition & StoreIDCondition & DeliveryTypeCondition & DeliverySlotCondition,
				
				    // // Format the query with dynamic start and end times for yesterday in IST
				 
				    Query = "    let currentUser = current_principal_details()['UserPrincipalName'];
				    let userCities = toscalar(
				        Anobis_RLS_MV
				        | where UserEmail_ID == currentUser
				        | summarize make_list(city)
				    );
				    let hasAllAccess = toscalar(
				        Anobis_RLS_MV
				        | where UserEmail_ID == currentUser and city == 'cities'
				        | count
				    );
				    let current_week = " & TableName & " 
				    | where created_at_ist >= datetime('"& StartOfLast7DaysFormatted & "') 
				    | where created_at_ist < datetime('"& EndofCurrentDateFormatted &"')
				      and ((hasAllAccess > 0) or (city in (userCities)))
				    " & CombinedFilters & "
				    | extend counter = case(order_level != 'ORDER_LEVEL_LEAF' or isempty(order_level), 1, 0) 
				    | where dp_order_type == 'INSTAMART' and (status == 'DELIVERY_DELIVERED' or status == 'COMPLETED') 
				    | summarize COUNT_id = round(sum(gmv)/sum(counter), 2) by bin(created_at_ist, 1d)
				    | extend time_period='Daily Trend'
				    | project COUNT_id, created_at_ist, time_period;
				    let last_week = " & TableName & " 
				    | where created_at_ist >= datetime('"& StartOfLastWeekFormatted & "')
				    | where created_at_ist < datetime('"& EndofLastWeekFormatted &"')
				      and ((hasAllAccess > 0) or (city in (userCities)))
				    " & CombinedFilters & "
				    | extend counter = case(order_level != 'ORDER_LEVEL_LEAF' or isempty(order_level), 1, 0) 
				    | where dp_order_type == 'INSTAMART' and (status == 'DELIVERY_DELIVERED' or status == 'COMPLETED')
				    | extend created_at_ist = datetime_add('hour', 168, created_at_ist)
				    | summarize COUNT_id = round(sum(gmv)/sum(counter), 2) by bin(created_at_ist, 1d)
				    | extend time_period='1 Week Ago'
				    | project COUNT_id, created_at_ist, time_period;
				    current_week
				    | union last_week",
				
				        // // Get data from Azure Data Explorer with the dynamic query
				    Source = AzureDataExplorer.Contents(Cluster,DB, Query, [MaxRows=null, MaxSize=null, NoTruncate=null, AdditionalSetStatements=null]),
				 #"Inserted Day of Week" = Table.AddColumn(Source, "Day of Week", each Date.DayOfWeek([created_at_ist]), Int64.Type),
				    #"Added Conditional Column" = Table.AddColumn(#"Inserted Day of Week", "Week", each if [Day of Week] = 1 then "Mon" else if [Day of Week] = 2 then "Tue" else if [Day of Week] = 3 then "Wed" else if [Day of Week] = 4 then "Thur" else if [Day of Week] = 5 then "Fri" else if [Day of Week] = 6 then "Sat" else if [Day of Week] = 0 then "Sun" else null),
				    #"Changed Type" = Table.TransformColumnTypes(#"Added Conditional Column",{{"created_at_ist", type datetime}, {"Week", type text}})
				in
				    #"Changed Type"
				```

	annotation PBI_NavigationStepName = Navigation

	annotation PBI_ResultType = Table

